import java.util.stream.Collectors

plugins {
    id('java')
    id('org.jetbrains.intellij') version '1.7.0'
    id("org.jetbrains.kotlin.jvm") version "1.7.10"
    id('com.adarshr.test-logger') version '3.1.0'
    // Gradle Changelog Plugin
    id("org.jetbrains.changelog") version "1.3.1"
    // Gradle Qodana Plugin
    id("org.jetbrains.qodana") version "0.1.13"
}

group 'com.reloadware'
version '0.8.3' //  # RwRender: version '{{ ctx.version }}' //
group = project.findProperty("pluginGroup")
version = project.findProperty("pluginVersion")

def intellijVersion = System.getenv("AT_WTIOIT_PYCHARM_PLUGIN_testVersion") ?: project.findProperty("platformVersion")
def intellijType = System.getenv("AT_WTIOIT_PYCHARM_PLUGIN_testType") ?: project.findProperty("platformType")
def intellijPlugins = Arrays.stream(project.findProperty("platformPlugins").split(',')).map(String::trim).filter((s) -> s.length() > 0).collect(Collectors.toList())
def buildDownloadSources = project.findProperty("platformDownloadSources").toBoolean()

def intellijMinorVersion = intellijVersion.substring(0, 6)
if (intellijVersion != '2019.2' && intellijType == 'PC') {
    // only PyCharm 2019.2 seems to provide com.jetbrains.python.psi itself
    intellijPlugins.add('PythonCore')
    // see https://plugins.jetbrains.com/plugin/7322-python-community-edition/versions
} else if (intellijVersion != '2019.2' && intellijType == 'PY') {
    // PyCharm Professional uses another python plugin
    intellijPlugins.add('Pythonid')
    // see https://plugins.jetbrains.com/plugin/631-python/versions
}
if (intellijType == 'PY' || intellijMinorVersion in ['2019.2', '2019.3','2020.1', '2020.2', '2020.3', '2021.1']) {
    // we can get no sources for pycharm professional
    // or versions where the source code does not reside in ivy
    // (2021 JCenter shut down and it seems that JetBrains did not publish/migrate older sources to ivy)
    buildDownloadSources = false
}
if (intellijType == 'PY' && intellijMinorVersion in ['2022.1'] ) {
    // https://youtrack.jetbrains.com/issue/IJSDK-1427/Running-tests-for-PyCharm-Professional-is-not-working
    // testing with PY since 2022.1 requires to also add com.intellij.platform.images (automatically provided in PC)
    intellijPlugins.add('com.intellij.platform.images')
}

repositories {
    mavenCentral()
    maven {
        url "https://www.jetbrains.com/intellij-repository/releases"
    }
    maven {
        url "https://www.jetbrains.com/intellij-repository/snapshots"
    }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://repository.jboss.org/nexus/content/groups/public'
    }
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

def remoteRobotVersion = "0.11.7"

dependencies {
    implementation('io.sentry:sentry:6.4.1') {
        exclude group: 'org.slf4j'
    }

    implementation(
            'net.lingala.zip4j:zip4j:2.11.1',
            'commons-io:commons-io:2.11.0',
    )

    testImplementation(
            'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion,
            'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion,
            'com.squareup.okhttp3:logging-interceptor:4.10.0',
            "com.redhat.devtools.intellij:intellij-common:1.1.0",
            'org.junit.jupiter:junit-jupiter-api:5.9.0',
            'com.redhat.devtools.intellij:intellij-common-ui-test-library:0.2.0',
            'com.automation-remarks:video-recorder-junit5:2.0',
            'org.mock-server:mockserver-netty:5.14.0',
            'org.mock-server:mockserver-client-java:5.14.0:shaded',
            'org.assertj:assertj-core:3.23.1',
            'org.powermock:powermock-core:2.0.9',
            'org.mockito:mockito-inline:4.7.0',
            'org.mockito:mockito-junit-jupiter:4.7.0'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    )
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'tests/java'
        }
        kotlin {
            srcDir 'tests/kotlin'
        }
        resources {
            srcDir 'tests/resources'
        }
    }
}

intellij {
    version = intellijVersion
    type = intellijType
    downloadSources = intellijDownloadSources as Boolean
    plugins = [intellijPlugin]
    updateSinceUntilBuild = false
}

// Pro
//intellij {
//    version = 'PY-2021.3.2'
//    type = 'PY'
//    downloadSources = true
//    plugins = ["python"]
//    updateSinceUntilBuild = false
//}

test {
    useJUnitPlatform()
}

downloadRobotServerPlugin {
    version.set(remoteRobotVersion)
}

runIdeForUiTests {
    systemProperty "robot-server.port", "8082"
    systemProperty "ide.mac.message.dialogs.as.sheets", "false"
    systemProperty "jb.privacy.policy.text", "<!--999.999-->"
    systemProperty "jb.consents.confirmation.enabled", "false"
    systemProperty "ide.mac.file.chooser.native", "false"
    systemProperty "apple.laf.useScreenMenuBar", "false"
    systemProperty "idea.trust.all.projects", "true"
    systemProperty "ide.show.tips.on.startup.default.value", "false"
}

publishPlugin {
    token = System.getenv("PYCHARM_HUBTOKEN")
}

runPluginVerifier {
    ideVersions = ["IU-222.3739.54","IU-211.7628.21", "IU-212.5712.43 ", "IU-213.7172.25", "IU-221.5080.93", "IU-203.8084.24"]
}

tasks.processResources{
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

runIde {
    jvmArgs '-Xmx8G'
    maxHeapSize = "8g"
}
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
